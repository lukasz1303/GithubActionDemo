plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.google.services)
    alias(libs.plugins.secrets.gradle.plugin)
}

def IS_CI = rootProject.ext.getEnvironmentProperty("CI").toBoolean()

android {
    namespace 'com.example.githubactionsdemo'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.githubactionsdemo"
        minSdk 28
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildFeatures {
        buildConfig = true
    }
    signingConfigs {
        release {
            storeFile file(System.getenv("RUNNER_TEMP") + "/keystore.jks")
            storePassword rootProject.ext.getEnvironmentProperty("ANDROID_KEYSTORE_PASSWORD")
            keyAlias rootProject.ext.getEnvironmentProperty("ANDROID_KEY_ALIAS")
            keyPassword rootProject.ext.getEnvironmentProperty("ANDROID_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            debuggable true
            buildConfigField('String', 'TestVariantField', '"Variant Debug"')
            applicationIdSuffix = ".dev"
            versionNameSuffix ' dev'
            signingConfig signingConfigs.debug
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField('String', 'TestVariantField', '"Variant Release"')
            if (IS_CI) {
                signingConfig = signingConfigs.release
            } else {
                signingConfig = signingConfigs.debug
            }
        }
        staging {
            initWith debug
            buildConfigField('String', 'TestVariantField', '"Variant Staging"')
            applicationIdSuffix = ".staging"
            versionNameSuffix ' staging'
            signingConfig signingConfigs.release
        }
        qa {
            initWith release
            buildConfigField('String', 'TestVariantField', '"Variant QA"')
            applicationIdSuffix = ".staging"
            versionNameSuffix ' qa'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation(platform("com.google.firebase:firebase-bom:33.1.2"))

    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
}